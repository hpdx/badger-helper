apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

// Maven Group ID
group = "com.anbetter"
// library的版本号，后面每次更新都需要更改这个值
version = "1.0.0"

// 生成jar包的task
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

// 生成jarDoc的task
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options.encoding = "UTF-8"
    failOnError false // 忽略注释语法错误，如果用jdk1.8你的注释写的不规范就编译不过。
}

// 生成javaDoc的jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

install {
    repositories.mavenInstaller {
        pom.project {
            name 'badger-helper'
            description 'Android在桌面图标上显示角标'
            url 'https://github.com/hpdx/badger-helper'
            inceptionYear '2017'

            packaging 'aar'
            groupId 'com.anbetter'
            artifactId 'badger-helper'
            version '1.0.0'

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            scm {
                connection 'https://github.com/hpdx/badger-helper.git' // Git仓库地址。
                developerConnection 'https://github.com/hpdx/badger-helper.git' // Git仓库地址。
                url 'https://github.com/hpdx/badger-helper' // 项目主页。
            }

            developers {
                developer {
                    id 'android_ls'
                    name 'android_ls'
                    email 'android_ls@163.com'
                }
            }
        }
    }
}

Properties properties = new Properties()
// 读取properties的配置信息
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'badger-helper'
        userOrg = user
        licenses = ['Apache-2.0']
        websiteUrl = 'https://github.com/hpdx/badger-helper'
        vcsUrl = 'https://github.com/hpdx/badger-helper.git'
        publish = true
        publicDownloadNumbers = true
    }
}